# Plot a Gaussian Distribution

This program demonstrates event injection and drawing with SVGs.

Create an svg to draw into.

```
bind @browser
  [#svg viewBox: "0 0 50 2", width: 200, height: 200]
```

Rescale the svg area based on the maximum value in a histogram slot. Most browsers seem to also use this to rescale x to keep the aspect ratio constant.

```
search
  [#slots x total]
  m = max[value: total, given: total]

search @browser
  s = [#svg]

bind @browser
  s.viewBox := "0 0 50 {{m + 2}}"
```

For each click, generate a gaussian sample, use floor to bin it into histogram by integer.

```
search @event @session
  e = [#click]
  x = floor[value: gaussian[seed: e, σ: 10, μ: 25]]
  total = if [#slots x total] then total + 1 else 1

commit
  s = [#slots x]
  s.total := total
```

Draw the bins as black rectangles.

```
search
  [#slots x total]
  m = max[value: total, given: total]

search @browser
  s = [#svg]

commit @browser
  k = [#rect x width: 1, stroke-width: 1, stroke: "black"]
  k.y := m - total
  k.height := total
  s.children += k
```

Inject a click once for each 1/60s.

```
search
   [#time frames]

bind @event
   [#click frames]
```
